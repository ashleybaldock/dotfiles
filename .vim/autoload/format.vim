if exists("g:mayhem_autoloaded_format") || &cp
  finish
endif
let g:mayhem_autoloaded_format = 1

"
" See: ../plugin/format.vim
"

let s:combase = get(g:, 'mayhem_unicode_combine_default', 'â—Œ')

let s:f_number = #{
      \ norm:  '0123456789',
      \ vs16:  '0ï¸1ï¸2ï¸3ï¸4ï¸5ï¸6ï¸7ï¸8ï¸9ï¸',
      \ sans:  'ðŸ¢ðŸ£ðŸ¤ðŸ¥ðŸ¦ðŸ§ðŸ¨ðŸ©ðŸªðŸ«',
      \ sansb: 'ðŸ¬ðŸ­ðŸ®ðŸ¯ðŸ°ðŸ±ðŸ²ðŸ³ðŸ´ðŸµ',
      \ sup:   'â°Â¹Â²Â³â´âµâ¶â·â¸â¹',
      \ sub:   'â‚€â‚â‚‚â‚ƒâ‚„â‚…â‚†â‚‡â‚ˆâ‚‰',
      \ sub16: 'â‚€ï¸â‚ï¸â‚‚ï¸â‚ƒï¸â‚„ï¸â‚…ï¸â‚†ï¸â‚‡ï¸â‚ˆï¸â‚‰ï¸',
      \ mono:  'ðŸ¶ðŸ·ðŸ¸ðŸ¹ðŸºðŸ»ðŸ¼ðŸ½ðŸ¾ðŸ¿',
      \ fullw: 'ï¼ï¼‘ï¼’ï¼“ï¼”ï¼•ï¼–ï¼—ï¼˜ï¼™',
      \ circ: 'â“ªâ‘ â‘¡â‘¢â‘£â‘¤â‘¥â‘¦â‘§â‘¨',
      \ paren: '0â‘´â‘µâ‘¶â‘·â‘¸â‘¹â‘ºâ‘»â‘¼',
      \ dot: '0â’ˆâ’‰â’Šâ’‹â’Œâ’â’Žâ’â’',
      \}
function! format#numbers(str, format = 'sans') abort
  return (a:str .. '')
        \->substitute('[0-9]', { 
        \ m -> get(s:f_number, a:format, '0123456789')
        \      ->strgetchar(m[0]->str2nr())->nr2char()
        \}, 'g')
endfunc

let s:f_session = [[
      \'0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ',
      \'ðŸ¶ðŸ·ðŸ¸ðŸ¹ðŸºðŸ»ðŸ¼ðŸ½ðŸ¾ðŸ¿ð™°ð™±ð™²ð™³ð™´ð™µð™¶ð™·ð™¸ð™¹ð™ºð™»ð™¼ð™½ð™¾ð™¿ðš€ðšðš‚ðšƒðš„ðš…ðš†ðš‡ðšˆðš‰', '', ''], [
      \'abcdefghijklmnopqrstuvwxyz',
      \'ðšŠðš‹ðšŒðšðšŽðšðšðš‘ðš’ðš“ðš”ðš•ðš–ðš—ðš˜ðš™ðššðš›ðšœðšðšžðšŸðš ðš¡ðš¢ðš£', '', 'â€Š'], [
      \' ',
      \'â€„', '', 'â€„']]

let s:f_session_lookup = {}
for [j, part] in s:f_session->items()
  let [_from, _to, prefix, suffix] = part
  let to = _to->split('\zs')
  for [i, f] in _from->split('\zs')->items()
    let s:f_session_lookup[f] = prefix .. to[i] .. suffix
  endfor
endfor

"
" Attempt to format input strings so they render in OSX menus as 'monospaced'
"
" [0-9A-Z]: replaced with equivalent math monospaced char   'A' -> 'ð™°'
" [a-z]: ditto, plus a hair space                           'a' -> 'ðšŠâ€Š'
" [ ]: replaced with 2 3perem spaces                        ' ' -> 'â€„â€„'
" â€¦anything else: replaced with 
"
function! format#session(str) abort
  return a:str->split('\zs')->map({i, c -> get(s:f_session_lookup, c, 'â°')})->join('')
endfunc


let s:replace = {_, v -> '['..typename(v)..']'}
let s:identity = {_, v -> v}
let s:typemap = {
      \  string(v:t_number): s:identity
      \, string(v:t_string): {_, v -> substitute(v, '\n', '\\n', 'g')}
      \, string(v:t_func): s:replace
      \, string(v:t_list): {_, v -> v->map(s:lookup)}
      \, string(v:t_dict): {_, v -> v->map(s:lookup)}
      \, string(v:t_float): s:identity
      \, string(v:t_bool): s:identity
      \, string(v:t_none): {_, v -> 'null'}
      \, string(v:t_job): s:replace
      \, string(v:t_channel): s:replace
      \, string(v:t_blob): s:replace
      \, string(v:t_class): s:replace
      \, string(v:t_object): s:replace
      \, string(v:t_typealias): s:replace
      \, string(v:t_enum): s:replace
      \, string(v:t_enumvalue): s:replace
      \ }
function s:Lookup(key, val)
  return get(s:typemap, string(type(a:val)), s:replace)(a:key, a:val)
endfunc
let s:lookup = function('s:Lookup')

"
" Turn a Vim dict into a JSON, taking care of any pesky Funcrefs
"
function s:DictToJson(someDict) abort
  return deepcopy(a:someDict)->map(s:lookup)->json_encode()
endfunc
"
" Runs prettier on the input JSON string
"
function format#json(jsonString) abort
  return systemlist('npx prettier --stdin-filepath nameless.json', a:jsonString)
endfunc
"
" Turn dict into JSON and then prettier it
"
function format#dict2json(dict) abort
  return format#json(s:DictToJson(a:dict))
endfunc
"
" Apply code beautification to the contents of a buffer (or part thereof)
"
let s:filetypemap = #{
      \ typescriptreact: 'tsx',
      \}
function format#buffer(bufnr = bufnr(), filetype = get(&l, 'filetype', 'html')) range abort
  let filetypeext = get(g:mayhem_type_ext_map, a:filetype, a:filetype)
  let filename = expand('%') ?? 'nameless'
  let stdinpath = shellescape(filename .. '.' .. filetypeext)

  exec a:firstline .. ',' .. a:lastline .. '!npx prettier --stdin-filepath ' .. stdinpath
endfunc

function format#list(text, filetype = 'html') abort
  let joined = type(a:text) == type([]) ? join(a:text, "\n") : a:text
  let filetypeext = get(g:mayhem_type_ext_map, a:filetype, a:filetype)

  return systemlist('npx prettier --stdin-filepath nameless.' .. filetypeext, joined)
endfunc

function format#FixedWidthFont(text)
  " '<,'>s/\u/\=nr2char(char2nr(submatch(0)) + 0x1D62F)/g
  " '<,'>s/\l/\=nr2char(char2nr(submatch(0)) + 0x1D629)/g
  return substitute(a:text,
        \ '\u', '\=char#base(submatch(0)) + 0x1D62F)', 'g')
        \->substitute(
        \ '\l', '\=char#base(submatch(0)) + 0x1D629)', 'g')
endfunc

function format#timeSince(eventtime)
  let ds = localtime() - a:eventtime
  if ds < 1
    return 'the future'
  endif
  if ds < 60
    return 'the last minute'
  endif
  if ds < 3600
    return 'the last hour'
  endif
  if ds < 86400
    return 'the last day'
  endif
  if ds < 604800
    return 'the last week'
  endif
  if ds < 2629743
    return 'the last month'
  endif
  if ds < 31556926
    return 'the last year'
  endif
  return 'over a year ago'
endfunc

"
" Format numbers with SI prefixes in a way that is pleasing to humans
"
function format#SIPrefixDecimal(n) abort
  let prefixes = ['', 'k', 'M', 'G', 'T']
  let cur = a:n
  let i = 0
  while cur > 1000 && i < len(prefixes) - 1
    let cur = cur / 1000.0
    let i = i + 1
  endwhile

  return printf("%4.1f%s", cur, prefixes[i])
endfunc

"
" Format (combined) character for display, showing
" sequence of base and combining characters
"
" e.g. format#charSplit('aÌ²')  ->  a â—ŒÌ²   
"
" Uses base display character defined in: g:mayhem_unicode_combine_default
"
function format#splitChar(str) abort
  return char#split(a:str)
        \->map({i, v -> i == 0 ? char2nr(v) : [char2nr(s:combase), char2nr(v)]})
        \->flatten()
        \->list2str()
endfunc 
